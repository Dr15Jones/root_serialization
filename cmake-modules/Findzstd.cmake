if (NOT zstd_FOUND)
  if (zstd_FIND_REQUIRED)
    set(_zstd_FIND_REQUIRED ${zstd_FIND_REQUIRED})
    unset(zstd_FIND_REQUIRED)
  else()
    unset(_zstd_FIND_REQUIRED)
  endif()
  find_package(zstd CONFIG QUIET)
  if (_zstd_FIND_REQUIRED)
    set(zstd_FIND_REQUIRED ${_zstd_FIND_REQUIRED})
    unset(_zstd_FIND_REQUIRED)
  endif()
  if (zstd_FOUND)
    set(_zstd_config_mode CONFIG_MODE)
  else()
    unset(_zstd_config_mode)
  endif()
endif()

if (NOT zstd_FOUND)
  find_library(_zstd_lib zstd)
  if (_zstd_lib)
    add_library(zstd::libzstd_shared SHARED IMPORTED)
    set_target_properties(zstd::libzstd_shared
      PROPERTIES IMPORTED_LOCATION ${_zstd_lib})
    set(zstd_FOUND TRUE)
    set(ZSTD_FOUND TRUE)
  endif()
endif()

set(zstd_FIND_REQUIRED ${_zstd_FIND_REQUIRED})
include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(zstd ${_zstd_config_mode}
  REQUIRED_VARS zstd_FOUND)

unset(_zstd_FIND_REQUIRED)
unset(_zstd_lib CACHE)
unset(_zstd_config_mode)
